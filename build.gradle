buildscript {
    ext {
        vertxVersion = '3.4.2'
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

repositories {
    jcenter()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'one.valuelogic'

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    compile "io.vertx:vertx-config:${vertxVersion}"
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile "io.vertx:vertx-unit:${vertxVersion}"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.name
                packaging 'jar'
                artifactId project.name
                description 'vertx config based on environmental variables'
                url 'https://github.com/valuelogic/vertx-env-config'

                scm {
                    connection 'scm:git:https://github.com/valuelogic/vertx-env-config.git'
                    developerConnection 'scm:git:git@github.com:valuelogic/vertx-env-config.git'
                    url 'https://github.com/valuelogic/vertx-env-config'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'czterocyty'
                        name 'Adam Domanski'
                        email 'adomanski@gmail.com'
                    }
                }
            }
        }
    }
}

